defaultTasks 'all'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

configurations {
    jflex
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    src          = new File(project.projectDir, 'src')
    dist         = new File(project.projectDir, 'dist')
    generatedsrc = new File(project.buildDir, '/generatedsrc')
    workingdirs  = [project.buildDir, generatedsrc, dist]
}

dependencies {
    List common = [
            'org.apache.lucene:lucene-core:3.0.2',
            'org.apache.lucene:lucene-spellchecker:3.0.2',
            'org.apache.bcel:bcel:5.2',
            'org.apache.ant:ant:1.8.1'
    ]
    jflex 'de.jflex:maven-jflex-plugin:1.4.3'
    compile common
    compile 'javax.servlet:servlet-api:2.5'
    runtime common
    testCompile 'junit:junit:4.8.2'
}

task cleanup << {
    delete project.ext.workingdirs
}

task prepare << {
    project.ext.workingdirs.each {
        it.mkdirs()
    }
}

task jflex(dependsOn: prepare) << {
    ant.taskdef(name: 'jflex', classname: 'JFlex.anttask.JFlexTask', classpath: configurations.jflex.asPath)

    fileTree(dir: project.ext.src).include('**/*.lex').each {
        ant.jflex(destdir: project.ext.generatedsrc, file: it)
    }
}

compileJava {
    dependsOn jflex
}

clean {
    dependsOn cleanup
}

sourceSets {
    main {
        java {
            srcDir project.ext.src
            srcDir project.ext.generatedsrc
        }
    }
    test {
        java {
            srcDir new File(project.projectDir, '/test/java')
        }
        resources {
            srcDir project.ext.src
            srcDir new File(project.projectDir, '/test/java')
            srcDir new File(project.projectDir, '/test/data')
        }
    }
}

jar {
    destinationDir project.ext.dist
    manifest {
        attributes('Main-Class': 'org.opensolaris.opengrok.index.Indexer')
    }
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
}

task prepareWar(dependsOn: jar) << {
    File destWar = new File(project.buildDir, 'war')
    File baseWar = new File(project.projectDir, 'web')
    copy {
        from baseWar
        into destWar
    }
    copy {
        from jar.archivePath
        into new File(destWar, 'WEB-INF/lib')
    }
}

war {
    destinationDir project.ext.dist
    from new File(project.buildDir, 'war')
}

task runIndexer(dependsOn: jar) << {
    File scRoot   = file(project.buildDir, 'sc')
    File scData   = file(scRoot, 'data')
    File scEtc    = file(scRoot, 'etc')
    File scConfig = file(scEtc, 'configuration.xml')

    [scData, scEtc].each {
        it.mkdirs()
    }

    ant.java(jar: jar.archivePath, fork: true) {
        arg(value: '-s')
        arg(value: project.ext.src.absolutePath)
        arg(value: '-d')
        arg(value: scData.absolutePath)
        arg(value: '-W')
        arg(value: scConfig.absolutePath)
    }
}

task all(dependsOn: [build, war, runIndexer]) {
}
