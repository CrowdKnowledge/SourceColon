defaultTasks 'all'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

configurations {
    generatesrc
    runtime {
        extendsFrom compile
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    base         = '.'
    src          = new File(base, 'src')
    workspace    = new File(base, 'build')
    dist         = new File(base, 'dist')
    generatedsrc = new File(workspace, '/generatedsrc')
    workingdirs  = [workspace, generatedsrc, dist]
}

dependencies {
    generatesrc 'de.jflex:maven-jflex-plugin:1.4.3'
    compile 'org.apache.lucene:lucene-core:3.0.2'
    compile 'org.apache.lucene:lucene-spellchecker:3.0.2'
    compile 'org.apache.bcel:bcel:5.2'
    compile 'org.apache.ant:ant:1.8.1'
    compile 'javax.servlet:servlet-api:2.5'
    testCompile 'junit:junit:4.8.2'
}

task cleanup << {
    delete project.ext.workingdirs
}

task prepare << {
    project.ext.workingdirs.each {
        it.mkdirs()
    }
}

task jflex(dependsOn: prepare) << {
    ant.taskdef(name: 'jflex', classname: 'JFlex.anttask.JFlexTask', classpath: configurations.generatesrc.asPath)

    fileTree(dir: project.ext.src).include('**/*.lex').each {
        ant.jflex(destdir: project.ext.generatedsrc, file: it)
    }
}

compileJava {
    dependsOn jflex
}

clean {
    dependsOn cleanup
}

sourceSets {
    main {
        java {
            srcDir project.ext.src
            srcDir project.ext.generatedsrc
        }
    }
    test {
        java {
            srcDir project.base + '/test/java'
        }
        resources {
            srcDir project.ext.src
            srcDir project.base + '/test/java'
            srcDir project.base + '/test/data'
        }
    }
}

jar {
    destinationDir project.ext.dist
    manifest {
        attributes('Main-Class': 'org.opensolaris.opengrok.index.Indexer')
    }
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
}

war {
    from project.base + '/war'
}

task runIndexer(dependsOn: jar) << {
    File scRoot   = new File(project.ext.workspace, 'sc')
    File scData   = new File(scRoot, 'data')
    File scEtc    = new File(scRoot, 'etc')
    File scConfig = new File(scEtc, 'configuration.xml')

    [scData, scEtc].each {
        it.mkdirs()
    }

    ant.java(jar: project.ext.dist.absolutePath + '/SourceColon.jar', fork: true) {
        arg(value: '-s')
        arg(value: project.ext.src.absolutePath)
        arg(value: '-d')
        arg(value: scData.absolutePath)
        arg(value: '-W')
        arg(value: scConfig.absolutePath)
    }
}

task all(dependsOn: [build, war, runIndexer]) {
}
